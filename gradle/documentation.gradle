task adaptorDocumentation(type: JavaExec) {
    description 'Generates html snippet with options of adaptors (used in the javadoc overview page)'
    group 'documentation'
    main = 'nl.esciencecenter.xenon.utils.AdaptorDocGenerator'
    File output = file("${buildDir}/overview.html")
    args = [output]
    classpath = sourceSets.docGenerator.runtimeClasspath
    outputs.file output
}

javadoc {
    options.overview = file("${buildDir}/overview.html")
    options.showFromPublic()
    options.setNoTimestamp(true)
    source = project(':core').sourceSets.main.allJava
    classpath = files(project(':core').sourceSets.main.compileClasspath)
}

task javadocDevel(type: Javadoc) {
    description 'Generates Javadoc API documentation for the main source code for Xenon developers.'
    group 'Documentation'
    options.showFromPrivate()
    options.overview = file("${buildDir}/overview.html")
    options.setNoTimestamp(true)
    title = javadoc.title + ' for Xenon developers'
    destinationDir = file("${project.docsDir}/javadoc-devel")
    source = project(':core').sourceSets.main.allJava
    classpath = files(project(':core').sourceSets.main.compileClasspath)
}

javadoc.dependsOn adaptorDocumentation
javadocDevel.dependsOn adaptorDocumentation
