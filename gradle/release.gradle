// Depends on plugins:
//    id 'com.jfrog.bintray' version '1.4'
//    id 'org.ajoberstar.grgit' version '1.3.2'
//    id 'org.ajoberstar.github-pages' version '1.3.2'
// Depends on distribution.gradle

//  PUBLISH ARTIFACTS
// ==================
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'xenon'
        name = 'xenon'
        desc = description
        userOrg = 'nlesc'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://nlesc.github.io/Xenon'
        vcsUrl = 'https://github.com/NLeSC/Xenon.git'
        issueTrackerUrl = 'https://github.com/NLeSC/Xenon/issues'
    }
    publications = ['MyPublication']
}

// PUBLISH DOCUMENTATION

def getDateOfVersion() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

ext.releaseDate = getDateOfVersion()
if (project.hasProperty('release.date')) {
    // overwrite release date with -Prelease.date=yyyy-MM-dd
    ext.releaseDate = project.property('release.date')
}

ext.versionPostFile = file("${project.docsDir}/${ext.releaseDate}-${version}.yml")

task versionPost(type: Exec) {
    description "Create a version post for website"
    group 'Publishing'
    outputs.file versionPostFile
    commandLine 'touch', versionPostFile.absolutePath
}

githubPages {
    repoUri = 'git@github.com:NLeSC/Xenon.git'
    pages {
        from(javadoc) {
            into "versions/$version/javadoc"
        }
        from(javadocDevel) {
            into "versions/$version/javadoc-devel"
        }
        from(versionPost) {
            into "_posts"
        }
    }
    deleteExistingFiles = false
}

tasks.publish.dependsOn 'bintrayUpload', 'publishGhPages'
