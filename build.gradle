// Build file which can only run when dependencies and plugins have been downloaded.

//    PLUGINS
// ==============
plugins {
    // Changes in the plugins section should be also be applied to build.offline.gradle
    id 'java'
    id 'distribution'
    // publishing
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.4'
    // ide
    id 'idea'
    id 'eclipse'
    // integration tests
    id 'org.hidetake.ssh' version '1.1.3'
    // code coverage
    id 'jacoco'
    // quality
    id 'pmd'
//    id 'org.sonarqube' version '2.3'
    // release
    // id 'org.ajoberstar.grgit' version '1.3.2'
    // id 'org.ajoberstar.github-pages' version '1.3.2'
    // license
    id 'com.github.hierynomus.license' version '0.12.1'
}

apply from: 'gradle/common.gradle'
apply from: 'gradle/integrationTest.gradle'
apply from: 'gradle/codestyle.gradle'
apply from: 'gradle/release.gradle'
apply from: 'gradle/license.gradle'

//  DEPENDENCIES
// ==============

// Online dependencies
repositories {
    mavenCentral()
    jcenter()
}

ext.jschVersion = '0.1.53'
ext.jschAgentproxyVersion = '0.0.9'
ext.apacheCommonsNetVersion = '3.3'

dependencies {
    // Xenon dependencies.
    // xenon/lib/**/*.jar to ./lib/
    compile group: 'com.jcraft', name: 'jsch', version: jschVersion
    compile group: 'com.jcraft', name: 'jsch.agentproxy.jsch', version: jschAgentproxyVersion
    compile group: 'com.jcraft', name: 'jsch.agentproxy.connector-factory', version: jschAgentproxyVersion
    compile group: 'commons-net', name: 'commons-net', version: apacheCommonsNetVersion
    compile group: 'com.google.code.findbugs', name: 'findbugs', version: '2.0.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.0.1'
    compile group: 'org.apache.jackrabbit', name: 'jackrabbit-webdav', version: '2.10.1'
    compile group: 'joda-time', name: 'joda-time', version: '2.8.1'

    // Runtime dependencies
    runtime group: 'ch.qos.logback', name: 'logback-core', version: '1.0.11'
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.11'

    // Testing dependencies
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

task downloadCompileDependencies(type: Copy) {
    from configurations.compile.files
    into compileLibDir
}

task downloadTestDependencies(type: Copy) {
    from (configurations.testCompile.files - configurations.compile.files)
    into testLibDir
}

task downloadRuntimeDependencies(type: Copy) {
    from (configurations.runtime.files - configurations.compile.files)
    into runtimeLibDir
}

task downloadCodestyleDependencies(type: Copy) {
    from configurations.pmd.files
    into codestyleLibDir
}

task downloadDependencies(dependsOn: [
        downloadCompileDependencies,
        downloadTestDependencies,
        downloadRuntimeDependencies,
        downloadCodestyleDependencies
    ]) {
    description "download all Java dependencies, as resolved by gradle, into the lib directory"
}
